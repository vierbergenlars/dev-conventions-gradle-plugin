pluginManagement {
    if (System.getenv("GITHUB_TOKEN")) {
        // Configure a custom repository when running on github actions
        repositories {
            maven {
                url = "https://maven.pkg.github.com/vierbergenlars/dev-conventions-gradle-plugin"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
            gradlePluginPortal()
        }
        // Replace dev-conventions plugins with the snapshot version
        resolutionStrategy {
            eachPlugin {
                if (requested.id.id.startsWith("be.vbgn.dev-conventions")) {
                    // Split current version, bump minor version, as reckon does by default to determine snapshot version
                    String[] versionParts = requested.version.split("\\.")
                    versionParts[1] = (versionParts[1].toInteger() + 1).toString()
                    useVersion(versionParts.join(".") + "-SNAPSHOT")
                }
            }
        }
    }
}
rootProject.name = 'dev-conventions-plugin'

include ':fake-ci-detect'

