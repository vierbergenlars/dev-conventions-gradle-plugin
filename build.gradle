import java.util.stream.Collectors

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'maven-publish'
    id 'org.ajoberstar.reckon' version '0.13.1'
    id 'org.sonarqube' version '3.3'
    id "com.gradle.plugin-publish" version "0.20.0"
    id 'nebula.integtest' version '9.3.0'
    id 'be.vbgn.dev-conventions.opinion' version '0.5.3'
    id 'be.vbgn.ci-detect' version '0.5.0'
}

group 'be.vbgn.gradle'

sourceCompatibility = 1.8

compileIntegTestJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

configurations {
    integTestAdditionalPlugins
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    integTestAdditionalPlugins project(":fake-ci-detect")
    integTestImplementation "junit:junit:4.13.1"
    integTestImplementation "commons-io:commons-io:2.11.0"
}

gradlePlugin {
    plugins {
        devConventions {
            id = 'be.vbgn.dev-conventions'
            implementationClass = 'be.vbgn.gradle.devconventions.DevConventionsPlugin'
        }
        devOpinions {
            id = 'be.vbgn.dev-conventions.opinion'
            implementationClass = 'be.vbgn.gradle.devconventions.DevOpinionsPlugin'
        }
    }
    testSourceSets(sourceSets.integTest)
}

integrationTest {
    dependsOn(configurations.integTestAdditionalPlugins)
    doFirst {
        systemProperty("be.vbgn.gradle.devconventions.integration.additionalPluginClasspath", configurations.integTestAdditionalPlugins.files.stream().map({ f -> f.path }).collect(Collectors.joining(":")))
    }
}

pluginBundle {
    vcsUrl = "https://github.com/vierbergenlars/dev-conventions-gradle-plugin"
    website = vcsUrl
    description = "A gradle plugin that automatically applies development conventions and/or opinions based on the plugins that are applied to your project"
    tags = ["convention", "integration"]
    plugins {
        devConventions {
            displayName = "Development conventions"
        }
        devOpinions {
            displayName = "Opinitionated development conventions"
        }
    }
}

