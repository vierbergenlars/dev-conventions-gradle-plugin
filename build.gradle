plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'maven-publish'
    id 'org.ajoberstar.reckon' version '0.13.0'
    id 'org.sonarqube' version '3.0'
    id "com.gradle.plugin-publish" version "0.12.0"
    id "com.github.harbby.gradle.serviceloader" version "1.1.5"
}

group 'be.vbgn.gradle'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

gradlePlugin {
    plugins {
        devConventions {
            id = 'be.vbgn.dev-conventions'
            implementationClass = 'be.vbgn.gradle.devConventions.DevConventionsPlugin'
        }
        devOpinions {
            id = 'be.vbgn.dev-conventions.opinions'
            implementationClass = 'be.vbgn.gradle.devConventions.DevOpinionsPlugin'
        }
    }
}

pluginBundle {
    vcsUrl = "https://github.com/vierbergenlars/dev-conventions-gradle-plugin"
    website = vcsUrl
    description = "A gradle plugin that automatically applies development conventions and/or opinions"
    tags = ["convention", "integration"]
    plugins {
        devConventions {
            displayName = "Development conventions"
        }
        devOpinions {
            displayName = "Opinitionated development conventions"
        }
    }
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

serviceLoader {
    serviceInterface "be.vbgn.gradle.devConventions.conventions.Convention"
    serviceInterface "be.vbgn.gradle.devConventions.conventions.Opinion"
}
